allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://kotlin.bintray.com/kotlinx" }
    }
}

buildscript {
    ext {
        kotlin_version = '1.3.11'
        kotlinio_version = '0.1.3'
        lint_version = '26.2.1'
        support_version = '28.0.0'
        android_test_version = '1.1.0'
        hamcrest_version = '1.3'
        build_version = '28.0.3'
        android_sdk_version = 28
        android_min_version = 16
    }
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.6'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-multiplatform'
apply plugin: 'com.google.protobuf'
apply plugin: 'checkstyle'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlinx-serialization'

/*
check.dependsOn 'checkstyle'

task checkstyle(type: Checkstyle) {
    source 'src'
    include '** /*.java'
    exclude '** /gen/**'
    classpath = files()
}
*/

dependencies {
    implementation ("com.android.support:appcompat-v7:$support_version") {
        exclude module: 'animated-vector-drawable'
        exclude module: 'support-fragment'
    }
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:support-annotations:$support_version"
    implementation "com.android.support:support-core-ui:$support_version"
    implementation ("com.android.support:support-core-utils:$support_version") {
        exclude module: 'support-compat'
    }
    implementation "com.android.support:support-vector-drawable:$support_version"
    implementation 'commons-io:commons-io:2.5'
    implementation 'org.apache.commons:commons-lang3:3.5'
    implementation 'com.google.protobuf:protobuf-lite:3.0.0'
    implementation 'com.neovisionaries:nv-i18n:1.23'
    implementation('org.simpleframework:simple-xml:2.7.1') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
    implementation project(':leaflet')
    implementation project(':material-design-icons')

    implementation 'com.github.bmelnychuk:atv:1.2.9'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    androidTestUtil "androidx.test:orchestrator:$android_test_version"
    androidTestImplementation "androidx.test:rules:$android_test_version"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    androidTestImplementation "androidx.test.ext:junit:1.0.0"
    androidTestImplementation "org.hamcrest:hamcrest-core:$hamcrest_version"
    androidTestImplementation "org.hamcrest:hamcrest-library:$hamcrest_version"

    lintChecks project(':lintchecks')
}

protobuf {
    protoc {
        // You still need protoc like in the non-Android case
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        javalite {
            // The codegen for lite comes as a separate artifact
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                // In most cases you don't need the full Java output
                // if you use the lite output.
                remove java
            }
            task.plugins {
                javalite { }
            }
        }
    }
}

task copyThirdPartyNotices(type: Copy) {
    from 'third_party/'
    into 'build/assets/third_party'
    eachFile { it.path = name }
    include 'NOTICE.**.txt'
}

kotlin {
    targets {
        fromPreset(presets.android, 'android')
    }

    sourceSets {
        commonMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib-common'
                api "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.9.1"
                api "org.jetbrains.kotlinx:kotlinx-io:$kotlinio_version"
            }
        }

        commonTest {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-test-common'
                api 'org.jetbrains.kotlin:kotlin-test-annotations-common'
                api 'org.jetbrains.kotlin:kotlin-stdlib-common'
                api "org.jetbrains.kotlinx:kotlinx-io:$kotlinio_version"
            }
        }

        androidMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-io-jvm:$kotlinio_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.0"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.9.1"
            }

            kotlin.srcDirs += 'src/jvmCommonMain/kotlin'
        }

        androidTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.0"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.9.1"
                implementation "org.jetbrains.kotlinx:kotlinx-io-jvm:$kotlinio_version"
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }
    }
}

android {
    compileSdkVersion android_sdk_version
    buildToolsVersion build_version

    defaultConfig {
        minSdkVersion android_min_version
        targetSdkVersion android_sdk_version
        testApplicationId "au.id.micolous.farebot.test"
        versionCode "git -C ${projectDir} rev-list --count HEAD".execute().text.trim().toInteger()
        versionName "git -C ${projectDir} describe --always --tags --dirty".execute().text.trim().replaceAll("^v", "")

        resConfigs ([
                // Update this whenever new language translations become available in Weblate.
                // Languages we support
                'en', 'en-rAU', 'en-rCA', 'en-rGB', 'en-rIE', 'en-rIN',  'en-rNZ', 'en-rUS',
                'de',
                'es',
                'fi',
                'fr',
                'ga',
                // Hebrew
                'he', 'iw',
                // Indonesian
                'id', 'in',
                'ja',
                'kab',
                'nb-rNO',
                'nl',
                'ru',
                'tr',
                'zh-rCN',
                'zh-rHK',
                'zh-rSG',
                'zh-rTW',

                // UI mode


        ])
        vectorDrawables {
            useSupportLibrary = true
            generatedDensities 'ldpi', 'mdpi', 'hdpi'
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    buildTypes {
        debug {
            pseudoLocalesEnabled true
        }
        dev.initWith(buildTypes.debug)
        dev {
            applicationIdSuffix '.dev'
            minifyEnabled true
            shrinkResources true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard.cfg'
            matchingFallbacks = ['debug']
        }
        release {
            minifyEnabled true
            shrinkResources true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard.cfg'
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name.equals("debug")) {
            variant.mergedFlavor.resourceConfigurations.add("en-rXA")
            variant.mergedFlavor.resourceConfigurations.add("ar-rXB")
        }
    }

    preBuild.dependsOn copyThirdPartyNotices

    sourceSets {
        androidTest.assets.srcDirs += [
                'src/commonTest/assets',
                'third_party/vpereira_bilhete/',
                'third_party/RejsekortReader/dumps/'
        ]
        main.assets.srcDirs += ['build/assets']
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/kotlinx-io.kotlin_module'
        exclude 'META-INF/atomicfu.kotlin_module'
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
}
androidExtensions {
    // Needed for kotlinx.android.parcel.Parcelize
    experimental = true
}
repositories {
    mavenCentral()
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}